# Maintainer: Julian Pollinger <julian@pollinger.dev>

pkgname=('rism')
pkgver=1.1.1
pkgrel=1
pkgdesc='Rust implementation of iSM'
arch=('x86_64')
url='https://github.com/neferin12/iSM'
license=('MIT')
depends=('z3')
makedepends=('cargo' 'clang')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/rust-v$pkgver.tar.gz")

prepare() {
    cd "iSM-rust-v$pkgver/rust"
    export RUSTUP_TOOLCHAIN=stable
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}


build() {
    cd "iSM-rust-v$pkgver/rust"
    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    cargo build --frozen --release --offline
}

package() {
    cd "iSM-rust-v$pkgver/rust"
    install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/$pkgname"
}
sha256sums=('46823700d6663cf59125d0ce5a78dfcb0a36fb30db6d1d83c6231835d27448f8')
