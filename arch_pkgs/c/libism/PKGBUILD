# Maintainer: Julian Pollinger <julian@pollinger.dev>

pkgname='libism'
pkgver=1.0.3
pkgrel=1
pkgdesc='Libraries for the C implementation of iSM'
arch=('x86_64')
url='https://github.com/neferin12/iSM'
license=('MIT')
depends=('glibc')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('c19929a4e563aff8783b57715df2ca6221704d3ade0480bd556ce55ad5fb6a23')
provides=("$pkgname=$pkgver")

build() {
    cmake -B build-$pkgver -S "iSM-$pkgver/c" \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -Wno-dev -DINSTALL_LIB=ON -DINSTALL_EXEC=OFF
    cmake --build build-$pkgver
}

check() {
    cmake --build build-$pkgver --target test
}

package() {
    DESTDIR="$pkgdir" cmake --install build-$pkgver
}
